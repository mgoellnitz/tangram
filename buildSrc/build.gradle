/**
 * 
 * Copyright 2013 Martin Goellnitz
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 * 
 */
println "Gradle: ${gradle.gradleVersion}"

defaultTasks 'clean', 'publishToMavenLocal'

apply plugin: 'groovy'
apply plugin: 'maven-publish'

String[] roots = ["${project.projectDir}/src/main/groovy"]
def versions = new GroovyScriptEngine(roots).loadScriptByName('TangramVersions.groovy').newInstance()

repositories {
  // most of the usual stuff
  mavenCentral()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.yahoo.platform.yui:yuicompressor:$versions.yui"
  // persistence APIs
  compile "$versions.jdo_api"
  compile "$versions.persistence_api"
  // for the direct datanucleus enhancer
  compile "org.datanucleus:datanucleus-core:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-api-jdo:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-api-jpa:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-enhancer:$versions.datanucleus_enhancer"
  // for the direct ecliselink enhancer
  compile "org.eclipse.persistence:org.eclipse.persistence.jpa:$versions.eclipselink"  
  // For the direct ebean enhancer
  compile "org.avaje.ebeanorm:avaje-ebeanorm-api:$versions.ebean_api"
  compile "org.avaje.ebeanorm:avaje-ebeanorm:$versions.ebean"
  compile "org.avaje.ebeanorm:avaje-ebeanorm-agent:$versions.ebean_agent"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'tangram'
      artifactId 'gradle-plugin'
      version '0.9-SNAPSHOT'
      from components.java
      artifact sourcesJar 
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}
