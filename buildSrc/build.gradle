/**
 *
 * Copyright 2013-2015 Martin Goellnitz
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 *
 */
import groovy.util.Node
import groovy.util.GroovyScriptEngine

plugins {
  id 'com.jfrog.artifactory' version '3.1.1'
  id 'com.gradle.plugin-publish' version '0.9.1'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

defaultTasks 'clean', 'publishToMavenLocal'

version = '1.0.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
  // most of the usual stuff
  jcenter()
}

String[] roots = ["${project.projectDir}/src/main/groovy"]
def versions = new GroovyScriptEngine(roots).loadScriptByName('org/tangram/gradle/plugin/TangramVersions.groovy').newInstance()

// Must for the time being be compiled with gradle 1.x/Groovy 1.x since most
// web based support plattforms don't support gradle 2 and Groovy 2.
dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.yahoo.platform.yui:yuicompressor:$versions.yui"
  // persistence APIs
  compile "$versions.jdo_api"
  compile "$versions.persistence_api"
  // for the direct datanucleus enhancer
  compile "org.datanucleus:datanucleus-core:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-api-jdo:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-api-jpa:$versions.datanucleus"
  compile "org.datanucleus:datanucleus-enhancer:$versions.datanucleus_enhancer"
  // for the direct ecliselink enhancer
  compile "org.eclipse.persistence:org.eclipse.persistence.jpa:$versions.eclipselink"
  // For the direct ebean enhancer
  compile "org.avaje.ebeanorm:avaje-ebeanorm:$versions.ebean"
  compile "org.avaje.ebeanorm:avaje-ebeanorm-agent:$versions.ebean_agent"
  // For the direct hibernate enhancer
  compile 'org.javassist:javassist:3.20.0-GA'
  compile "org.hibernate:hibernate-core:$versions.hibernate"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
  classifier = 'javadoc'
  from "${buildDir}/docs/groovydoc"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      if (gradle.gradleVersion != '1.6') {
        groupId 'tangram'
        artifactId 'gradle-plugin'
      }
      version '1.0.0'
      from components.java
      artifact sourcesJar
      artifact groovydocJar
      pom.withXml {
        def pom = asNode()
        pom.children().get(3).plus {
          new Node(pom, 'name', "Tangram Gradle plugin")
          new Node(pom, 'description', 'Object oriented templating and dynamic web development framework')
          new Node(pom, 'url', 'https://github.com/mgoellnitz/tangram/')
          def scm = new Node(pom, 'scm')
          scm.appendNode('connection', 'scm:git:git@github.com:mgoellnitz/tangram.git')
          scm.appendNode('developerConnection', 'scm:git:git@github.com:mgoellnitz/tangram.git')
          scm.appendNode('url', 'git@github.com:mgoellnitz/tangram.git')
          scm
          def license = new Node(pom, 'licenses').appendNode('license')
          license.appendNode('name', 'LGPL-3.0')
          license.appendNode('url', 'http://opensource.org/licenses/LGPL-3.0')
          license
          def developer = pom.appendNode('developers').appendNode('developer')
          developer.appendNode('name', 'Martin Goellnitz')
          developer.appendNode('organization', 'Provocon')
          developer.appendNode('organizationUrl', 'http://www.provocon.de/')
          developer
        }
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/mgoellnitz/tangram/'
  vcsUrl = 'https://github.com/mgoellnitz/tangram/'
  description = 'Tangram JPA, JDO, EBean, webapp overlay, and minifying plugin'
  tags = ['tangram', 'jdo', 'jpa', 'ebean', 'datanucleus', 'eclipselink', 'hibernate', 'openjpa', 'overlay', 'minify']

  plugins {
    greetingsPlugin {
      id = 'tangram.gradle.plugin'
      displayName = 'Tangram Gradle plugin'
    }
  }
}
