/** user serviceable parts */
// TODO: perhaps this should go somewhere else
targetMavenRepository = "file://localhost/C:/volatile/m2/repository/"
localMavenRepository = "file://localhost/C:/volatile/m2/repository/"
/** end user serviceable parts */

/* version section */
servlet_spec = "2.5"
jsp_spec = "2.0"
groovy_version = "1.8.3"
velocity_version = "1.7"
// TODO: find repo for 2.8.2
yui_version="2.4.6"
// TODO: yui compressor uses its own old version
rhino_version="1.7R3"
// TODO: find repo for 1.8.1.3
hsqldb_version="1.8.0.10"
jdo_api="javax.jdo:jdo2-api:2.3-eb"

springframework_version = "3.0.6.RELEASE"
springsecurity_version = "3.0.7.RELEASE"
// Datanucleus Version except for Google App Engine
// TODO: Where is the 3.0.2 enhancer?
datanucleus_version= "3.0.1"

appengine_version = "1.5.5"
/* version section end */

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'maven'

repositories {

  mavenLocal()

  maven { url localMavenRepository }
  
  // most of the usual stuff
  maven { url "http://repo1.maven.org/maven2" }
  // needed for latest spring framework and security versions
  maven { url "http://repository.springsource.com/maven/bundles/release" }
  // needed for latest datanucleus versions
  maven { url "http://www.datanucleus.org/downloads/maven2" }
  
  /* maybe unused
  maven { url "http://repository.jboss.org/maven2" }
  maven { url "http://repository.codehaus.org" }
  maven { url "http://fornax.itemis.de/nexus/content/repositories/thirdparty" }
  */
 
}

version = '0.6-SNAPSHOT'
basename = 'tangram'

// datanucleus enhancer doesnt support this, but it works!
sourceCompatibility = 1.6
targetCompatibility = 1.6

// our custom directory layout
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
}

// despite the short directory names we won't full blown archive names
archivesBaseName = basename+'-'+project.name

// avoid gradles default mimik to fiddle around with this directory
webAppDirName = 'disabled'

// we want jars and wars - in most cases
jar.enabled = true 

// needed for building jars and wars
configurations {
  libs 
  webapp
}

// needed for building jars and wars
artifacts {
  archives war
  libs jar
}

compileJava.doLast {
  // update build number after build 
  // do this here to avoid compiling without source changes since this here is a source change
  filename = "${project.rootDir.absolutePath}/${project.name}/src/$basename/${project.name}-build.properties".toString()
  Properties p = new Properties()
  try {
    FileInputStream fis = new FileInputStream(filename);
    p.load(fis)
    fis.close()
  } catch (Exception e) {
   // 
  }
  p.setProperty("version.date", ""+(new Date()))
  p.setProperty("version.build", ""+(Integer.parseInt(p.getProperty("version.build", "0"))+1))
  FileOutputStream fos = new FileOutputStream(filename)
  p.save(fos, " Tangram Build and System Information")
  fos.close()
  // JDO Enhancing is not really done when we start this - sorry - so we wait a little
  print "waiting 3s - which is normal in this project"
  Thread.currentThread().sleep(3000)
  println " - done"
}

/* TODO:
ant.taskdef(name:"yui-compressor", 
                 classname: "net.noha.tools.ant.yuicompressor.tasks.YuiCompressorTask",
                 classpath: configurations.compile.asPath+";../core/lib/yui-compressor-ant-task-0.5.jar")
*/

war.doFirst {
  // Strange way of overwriting things - it must be the first dependency
  iter = configurations.webapp.dependencies.iterator()
  if (iter.hasNext()) {
    firstWebappDependency = iter.next()
    if (firstWebappDependency instanceof org.gradle.api.artifacts.ExternalModuleDependency) {
      archiveFileName = configurations.webapp.asPath
      idx = archiveFileName.indexOf(';')
      if (idx >= 0) {
        archiveFileName = archiveFileName.substring(0, idx)
      } // if
      // println "unzipping $archiveFileName"
      ant.unzip(src: archiveFileName, dest: "$buildDir/target")  
    } // if 
  } // if 
  
  // TODO: YUICompressor!
  copy {
    from 'webapp'
    into "$buildDir/target"
    include '**/**'
  }
  
  into ('') {
    from "$buildDir/target"
    exclude 'WEB-INF/lib/**'
  }
}

war {
  classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
}

// just for debugging purposes
war.doLast {
  ant.unzip(src: war.archivePath, dest: "$buildDir/war")
}

uploadArchives {
  inputs.dir file("$buildDir/libs")
  repositories.mavenDeployer {
      repository(url: targetMavenRepository)
      // TODO: change this when we know how to rename sub projects
      // pom.artifactId = basename+'-'+project.name+'-webapp'
      pom.artifactId = project.name+'-webapp'
  }
}

uploadLibs {
  inputs.dir file("$buildDir/libs")
  repositories.mavenDeployer {
      repository(url: targetMavenRepository)
      // TODO: remove this when we know how to rename sub projects
      pom.artifactId = project.name
  }
}

task upload(dependsOn: [uploadArchives, uploadLibs]) {
}
