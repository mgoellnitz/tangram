/**
 * 
 * Copyright 2011 Martin Goellnitz
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 * 
 */

/* version section */
ext.servlet_spec = "2.5"
ext.jsp_spec = "2.0"
ext.groovy_version = "2.0.2"
ext.velocity_version = "1.7"
ext.yui_version="2.4.7"
// TODO: find maven repository for 1.8.1.3
ext.hsqldb_version="1.8.0.10"
ext.jdo_api="javax.jdo:jdo-api:3.0.1"

ext.springframework_version = "3.1.3.RELEASE"
ext.springsecurity_version = "3.1.3.RELEASE"
// Datanucleus Version except for Google App Engine
ext.datanucleus_version= "3.1.3"
// The byte code enhancer is not included in every version for some reason
ext.datanucleus_enhancer_version= "3.1.1"
ext.datanucleus_appengine_version= "2.1.1"

ext.appengine_version = "1.7.5"
/* version section end */

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'maven'
// apply plugin: 'code-quality'

repositories {
  // most of the usual stuff
  maven { url "http://repo1.maven.org/maven2" }
  // For the latest groovy stuff
  maven { url "http://repository.codehaus.org/org/codehaus/groovy" }
  // needed for latest spring framework and security versions
  maven { url "http://repository.springsource.com/maven/bundles/release" }
  // needed for latest datanucleus versions
  maven { url "http://www.datanucleus.org/downloads/maven2" }
}

version = '0.8-SNAPSHOT'
ext.basename = 'tangram'

// datanucleus enhancer doesn't support this, but it works!
sourceCompatibility = 1.6
targetCompatibility = 1.6

// Project specific default output directory
eclipse.classpath.conventionMapping.defaultOutputDir = { new File(project.projectDir, 'build/classes/main') }

// our custom directory layout
sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

// A place for a common checkstyle config file
ext.checkstyleConfigFileName="${project.rootDir.absolutePath}/codestyle.xml"

// despite the short directory names we won't full blown archive names
archivesBaseName = basename+'-'+project.name

// avoid gradles default mimik to fiddle around with this directory
webAppDirName = 'disabled'

// we want jars and wars - in most cases
jar.enabled = true 

// needed for building jars and wars
configurations {
  libs 
  webapp
}

// needed for building jars and wars
artifacts {
  archives war
  libs jar
}

compileJava.doLast {
  // update build number after build 
  // do this here to avoid compiling without source changes since this here is a source change
  println "Updating compile counter property files"
  ext.filename = "${project.rootDir.absolutePath}/${project.name}/src/$basename/${project.name}-build.properties".toString()
  Properties p = new Properties()
  try {
    FileInputStream fis = new FileInputStream(filename);
    p.load(fis)
    fis.close()
  } catch (Exception e) {
   // 
  }
  p.setProperty("version.date", ""+(new Date()))
  p.setProperty("version.build", ""+(Integer.parseInt(p.getProperty("version.build", "0"))+1))
  FileOutputStream fos = new FileOutputStream(filename)
  p.save(fos, " Tangram Build and System Information")
  fos.close()
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

// TODO: this looks a little bit redundnant...
buildscript {
    repositories {
        mavenLocal()
  
        // most of the usual stuff
        maven { url "http://repo1.maven.org/maven2" }
    }

    dependencies {
        // TODO: how to use the yui_version variable above?
        classpath "com.yahoo.platform.yui:yuicompressor:2.4.6"
    }
}

class JavaScriptMinify extends FilterReader {

    Reader inputReader

    public JavaScriptMinify(Reader inputReader) {
        super(inputReader);
        if (inputReader!=null) {
            try {
                com.yahoo.platform.yui.compressor.JavaScriptCompressor jsc = new com.yahoo.platform.yui.compressor.JavaScriptCompressor(inputReader, null);
                StringWriter out = new StringWriter();
                jsc.compress(out, 80, false, false, false, false);
                out.close();
                super.in = new StringReader(out.getBuffer().toString());
            } catch (Exception e) {
                ; // who cares
            } // try/catch
        } // if
    } // JavaScriptMinify()

} // JavaScriptMinify

class CSSMinify extends FilterReader {

    Reader inputReader

    public CSSMinify(Reader inputReader) {
        super(inputReader);
        if (inputReader!=null) {
            try {
                com.yahoo.platform.yui.compressor.CssCompressor csc = new com.yahoo.platform.yui.compressor.CssCompressor(inputReader);
                StringWriter out = new StringWriter();
                csc.compress(out, 0);
                out.close();
                super.in = new StringReader(out.getBuffer().toString());
            } catch (Exception e) {
                ; // who cares
            } // try/catch
        } // if
    } // CSSMinify()

} // CSSMinify

war.doFirst {
  // Strange way of overwriting things - it must be the first webapp dependency
  Object iter = configurations.webapp.dependencies.iterator()
  if (iter.hasNext()) {
    Object firstWebappDependency = iter.next()
    if (firstWebappDependency instanceof org.gradle.api.artifacts.ProjectDependency) {
      String archiveFileName = firstWebappDependency.dependencyProject.war.outputs.files.singleFile.absolutePath
      /*
      idx = archiveFileName.indexOf(';')
      if (idx >= 0) {
        archiveFileName = archiveFileName.substring(0, idx)
      } // if
      */
      ant.unzip(src: archiveFileName, dest: "$buildDir/target")  
    } else {
      println "WARNING: MISSING WAR TO ADD LOCAL FILES TO!"
    } // if 
  } // if 
  
  copy {
    from 'webapp'
    into "$buildDir/target"
    include '**/**'
    // exclude '**/*.js'
    exclude '**/*.css'
  }
  copy {
    from 'webapp'
    into "$buildDir/target"
    include '**/*.js'
    exclude 'editor/ckeditor/**'
    filter(JavaScriptMinify)
  }
  copy {
    from 'webapp'
    into "$buildDir/target"
    include '**/*.css'
    filter(CSSMinify)
  }
  
  into ('') {
    from "$buildDir/target"
    exclude 'WEB-INF/lib/**'
  }
}

war.dependsOn sourcesJar

war {
  // For some reason webapp files are not included in the inputs collection
  FileTree tree = fileTree(dir: 'webapp')
  inputs.files tree
  // And also the web-archive of the upstream project has to be added
  Object iter = configurations.webapp.dependencies.iterator()
  if (iter.hasNext()) {
    firstWebappDependency = iter.next()
    if (firstWebappDependency instanceof org.gradle.api.artifacts.ProjectDependency) {
      println "Adding "+firstWebappDependency.dependencyProject.war.outputs.files.singleFile.name
      inputs.file firstWebappDependency.dependencyProject.war.outputs.files.singleFile
    } else {
      println "WARNING: MISSING WAR TO ADD LOCAL FILES TO!"
    } // if 
  } // if 
  // classpath = jar.outputs.files + configurations.runtime - configurations.providedRuntime
  classpath = jar.outputs.files
}

// just for debugging purposes
war.doLast {
  ant.unzip(src: war.archivePath, dest: "$buildDir/war")
}

uploadArchives {
  inputs.dir file("$buildDir/libs")
  repositories.mavenDeployer {
      repository(url: repositories.mavenLocal().url)
      // TODO: change this when we know how to rename sub projects
      // depends on http://issues.gradle.org/browse/GRADLE-1135
      // pom.artifactId = basename+'-'+project.name+'-webapp'
      pom.artifactId = project.name+'-webapp'
  }
}

uploadLibs {
  inputs.dir file("$buildDir/libs")
  repositories.mavenDeployer {
      repository(url: repositories.mavenLocal().url)
      // TODO: remove this when we know how to rename sub projects
      // depends on http://issues.gradle.org/browse/GRADLE-1135
      pom.artifactId = project.name
  }
}

task upload(dependsOn: [uploadArchives, uploadLibs]) {
}

// println "project: "+project.name
